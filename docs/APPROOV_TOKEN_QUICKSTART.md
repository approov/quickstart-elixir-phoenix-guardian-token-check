# Approov Token Quickstart

This quickstart is for developers familiar with Phoenix who are looking for a quick intro into how they can add [Approov](https://approov.io) into an existing project. Therefore this will guide you through the necessary steps for adding Approov to an existing Elixir Phoenix API server.

## TOC - Table of Contents

* [Why?](#why)
* [How it Works?](#how-it-works)
* [Requirements](#requirements)
* [Approov Setup](#approov-setup)
* [Approov Token Check](#approov-token-check)
* [Try the Approov Integration Example](#try-the-approov-integration-example)


## Why?

To lock down your API server to your mobile app. Please read the brief summary in the [Approov Overview](/OVERVIEW.md#why) at the root of this repo or visit our [website](https://approov.io/product) for more details.

[TOC](#toc---table-of-contents)


## How it works?

For more background, see the [Approov Overview](/OVERVIEW.md#how-it-works) at the root of this repo.

The main functionality for the Approov token check is in the [Approov Token Plug](/src/approov-protected-server/token-check/hello/lib/hello_web/plugs/approov_token_plug.ex) module. Take a look at the `_verify_approov_token/1` function to see the simple code for the check.

[TOC](#toc---table-of-contents)


## Requirements

To complete this quickstart you will need both the Phoenix and the Approov CLI tool installed.

* Phoenix - Follow the official installation instructions from [here](https://hexdocs.pm/phoenix/installation.html#content)
* Approov CLI - Follow our [installation instructions](https://approov.io/docs/latest/approov-installation/#approov-tool) and read more about each command and its options in the [documentation reference](https://approov.io/docs/latest/approov-cli-tool-reference/)

[TOC](#toc---table-of-contents)


## Approov Setup

To use Approov with the Elixir Phoenix API server we need a small amount of configuration. First, Approov needs to know the API domain that will be protected. Second, the Elixir Phoenix API server needs the Approov Base64 encoded secret that will be used to verify the tokens generated by the Approov cloud service.

### Configure API Domain

Approov needs to know the domain name of the API for which it will issue tokens.

Add it with:

```bash
approov api -add your.api.domain.com
```

Adding the API domain also configures the [dynamic certificate pinning](https://approov.io/docs/latest/approov-usage-documentation/#approov-dynamic-pinning) setup, out of the box.

> **NOTE:** By default the pin is extracted from the public key of the leaf certificate served by the domain, as visible to the box executing the Approov CLI command and the Approov servers.

### Approov Secret

Approov tokens are signed with a symmetric secret. To verify tokens, we need to grab the secret using the [Approov secret command](https://approov.io/docs/latest/approov-cli-tool-reference/#secret-command) and plug it into the Elixir Phoenix API server environment to check the signatures of the [Approov Tokens](https://www.approov.io/docs/latest/approov-usage-documentation/#approov-tokens) that it processes.

First, enable your Approov `admin` role with:

```bash
eval `approov role admin`
````

Next, retrieve the Approov secret with:

```text
approov secret -get base64url
```

#### Set the Approov Secret

From your terminal, export the Approov secret into the environment:

```bash
export APPROOV_BASE64_SECRET=approov_base64_secret_here
```

To read the Approov secret from your app you need to add some code to the Elixir configuration, but Elixir has compile time and runtime configuration. To not duplicate the Approov secret configuration we recommend you to add it only in the runtime configuration.

##### From Elixir 1.11

If doesn't exist already, create the file `config/runtime.exs` and then add:

```elixir
import Config

approov_secret =
  System.get_env("APPROOV_BASE64_SECRET") ||
    raise "Environment variable APPROOV_BASE64_SECRET is missing."

config :YOUR_APP, YOUR_APP.ApproovTokenPlug,
  allowed_algos: ["HS256"],
  secret_key: Base.decode64!(approov_secret)
```

The runtime configuration will run every-time a release or a Mix project is started.

##### From Elixir 1.9 to 1.10

The runtime configuration in this versions is `config/releases.exs`, but with the difference that it only runs when a release is booted, not when a Mix project is started, where only the compile time configuration is used.

To avoid duplicating the code for reading the Approov secret in both configurations you may want to import the runtime configuration from within your compile time configuration.

Add to `config/releases.exs`:

```elixir
import Config

approov_secret =
  System.get_env("APPROOV_BASE64_SECRET") ||
    raise "Environment variable APPROOV_BASE64_SECRET is missing."

config :YOUR_APP, YOUR_APP.ApproovTokenPlug,
  allowed_algos: ["HS256"],
  secret_key: Base.decode64!(approov_secret)

config :YOUR_APP, YOUR_APP.Endpoint, server: true
```

Then import this file from `config/config.exs`:

```elixir
import_config "releases.exs"

# Import environment specific config. This must remain at the bottom
# of this file so it overrides the configuration defined above.
import_config "#{Mix.env()}.exs"
```

With this approach you are in fact emulating the Elixir `1.11` behaviour for the runtime configuration `config/runtime.exs`.

##### Until Elixir 1.8

Until Elixir `1.8` version no official way existed of reading values for each time the app was started from a release or a Mix project, and lots of different workarounds existed, and you can see some of them being discusses in the Elixir Forum, like in this [topic](https://elixirforum.com/t/phoenix-distillery-kubernetes-and-runtime-env-variables/13399).

[TOC](#toc---table-of-contents)

## Approov Token Check

To check the Approov token we will use the [ueberauth/guardian](https://github.com/ueberauth/guardian) package.

First, add the [Approov Token Plug](/src/approov-protected-server/token-check/hello/lib/hello_web/plugs/approov_token_plug.ex) module to your project at `lib/your_app_web/plugs/approov_token_plug.ex`:

```elixir
defmodule YOUR_APP.ApproovTokenPlug do
  require Logger

  ##############################################################################
  # Adhere to the Phoenix Module Plugs specification by implementing:
  #   * init/1
  #   * call/2
  #
  # @link https://hexdocs.pm/phoenix/plug.html#module-plugs
  ##############################################################################

  def init(options), do: options

  def call(conn, _options) do
    with {:ok, conn, _approov_token_claims} <- _verify_approov_token(conn) do
      conn
    else
      {:error, conn} ->
        conn |> _halt_connection()
    end
  end

  ##############################################################################
  # Inject Guardian functions and implement the required behaviour callbacks:
  #   * subject_for_token/2
  #   * resource_from_claims/2
  #
  # The required behaviour functions are not necessary in the context of
  # checking the Approov token, but required to be implemented in order to use
  # Guardian.
  ##############################################################################

  use Guardian, otp_app: :hello

  @impl true
  def subject_for_token(user, _claims), do: {:ok, to_string(user.id)}

  @impl true
  def resource_from_claims(claims), do: {:ok, claims["sub"]}

  defp _verify_approov_token(conn) do
    with [approov_token | _] <- Plug.Conn.get_req_header(conn, "approov-token"),
         {:ok, approov_token_claims} <- decode_and_verify(approov_token),
         :ok <- _verify_expiration(approov_token_claims) do
      {:ok, conn, approov_token_claims}
    else
      [] ->
        # You may want to add some logging here
        {:error, conn}

      {:error, reason} when is_atom(reason) ->
        # You may want to add some logging here
        {:error, conn}

      {:error, %ArgumentError{} = error} ->
        # You may want to add some logging here
        {:error, conn}

      {:error, error} ->
        # You may want to add some logging here
        {:error, conn}
    end
  end

  defp _verify_expiration(%{"exp" => timestamp}) do
    datetime = _timestamp_to_datetime(timestamp)
    now = DateTime.utc_now()

    case DateTime.compare(now, datetime) do
      :lt ->
        :ok

      _ ->
        {:error, :approov_token_expired}
    end
  end

  defp _verify_expiration(_claims) do
    {:error, :missing_exp_claim}
  end

  defp _timestamp_to_datetime(timestamp) when is_integer(timestamp) do
    DateTime.from_unix!(timestamp)
  end

  defp _timestamp_to_datetime(timestamp) when is_float(timestamp) do
    {timestamp, _decimals} = Integer.parse("#{timestamp}")
    DateTime.from_unix!(timestamp)
  end

  defp _halt_connection(conn) do
    conn
    |> Plug.Conn.put_status(401)
    |> Phoenix.Controller.json(%{})
    |> Plug.Conn.halt()
  end
end
```

> **NOTE:** When the Approov token validation fails we return a `401` with an empty body, because we don't want to give clues to an attacker about the reason the request failed, and you can go even further by returning a `400`.

Now, add the [Approov Token Plug](/src/approov-protected-server/token-check/hello/lib/hello_web/plugs/approov_token_plug.ex) to the `:api` pipeline on your Phoenix router `lib/your_app_web/router.ex`:

```elixir
pipeline :api do
  plug :accepts, ["json"]

  # Ideally you will not want to add any other Plug before the Approov Token
  # check to protect your server from wasting resources in processing requests
  # not having a valid Approov token. This increases availability for your
  # users during peak time or in the event of a DoS attack(We all know the
  # BEAM design allows to cope very well with this scenarios, but best to play
  # in the safe side).
  plug YourAppWeb.ApproovTokenPlug
end
```

A full working example for a simple Hello World server can be found at [src/approov-protected-server/token-check/hello](/src/approov-protected-server/token-check/hello).

[TOC](#toc---table-of-contents)


## Test your Approov Integration

The following examples below use cURL, but you can also use the [Postman Collection](/README.md#testing-with-postman) to make the API requests. Just remember that you need to adjust the urls and tokens defined in the collection to match your deployment. Alternatively, the above README also contains instructions for using the preset _dummy_ secret to test your Approov integration.

#### With Valid Approov Tokens

Generate a valid token example from the Approov Cloud service:

```bash
approov token -genExample your.api.domain.com
```

Then make the request with the generated token:

```bash
curl -i --request GET 'https://your.api.domain.com' \
  --header 'Approov-Token: APPROOV_TOKEN_EXAMPLE_HERE'
```

The request should be accepted. For example:

```text
HTTP/1.1 200

...

{"message": "Hello, World!"}
```

#### With Invalid Approov Tokens

Generate an invalid token example from the Approov Cloud service:

```bash
approov token -type invalid -genExample your.api.domain.com
```

Then make the request with the generated token:

```bash
curl -i --request GET 'https://your.api.domain.com' \
  --header 'Approov-Token: APPROOV_INVALID_TOKEN_EXAMPLE_HERE'
```

The above request should fail with an Unauthorized error. For example:

```text
HTTP/1.1 401

...

{}
```

[TOC](#toc---table-of-contents)


## Issues

If you find any issue while following our instructions then just report it [here](https://github.com/approov/quickstart-elixir-phoenix-guardian-token-check/issues), with the steps to reproduce it, and we will sort it out and/or guide you to the correct path.

[TOC](#toc---table-of-contents)


## Useful Links

If you wish to explore the Approov solution in more depth, then why not try one of the following links as a jumping off point:

* [Approov Free Trial](https://approov.io/signup)(no credit card needed)
* [Approov Get Started](https://approov.io/product/demo)
* [Approov QuickStarts](https://approov.io/docs/latest/approov-integration-examples/)
* [Approov Docs](https://approov.io/docs)
* [Approov Blog](https://approov.io/blog/)
* [Approov Resources](https://approov.io/resource/)
* [Approov Customer Stories](https://approov.io/customer)
* [Approov Support](https://approov.zendesk.com/hc/en-gb/requests/new)
* [About Us](https://approov.io/company)
* [Contact Us](https://approov.io/contact)

[TOC](#toc---table-of-contents)
